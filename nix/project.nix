{ pkgs }:
let
  planProject = pkgs.haskell-nix.cabalProject' {
    src = pkgs.haskell-nix.cleanSourceHaskell {
      src = ./..;
      name = "tmux-ws";
    };
    compiler-nix-name = "ghc984";
    modules = [{ packages.tmux-ws.flags.development-warnings = true; }];
    shell = { ... }: {
      tools = {
        cabal = { };
        fourmolu = { };
        hlint = { };
        hoogle = { };
        cabal-fmt = { };
      };
      buildInputs = with pkgs; [
        just
        nixfmt-classic
        shellcheck
        stgit
        tmux
        websocat
        purs
        spago-unstable
        purs-tidy-bin.purs-tidy-0_10_0
        esbuild
        nodejs_22
      ];
    };
  };
  indexStateHashes = import pkgs.haskell-nix.indexStateHashesPath;
  suitableIndexStates =
    builtins.filter (state: state > planProject.index-state-max)
    (builtins.attrNames indexStateHashes);
  cachedIndexState = if suitableIndexStates == [ ] then
    planProject.index-state-max
  else
    pkgs.lib.head suitableIndexStates;
  indexSha256 = indexStateHashes.${cachedIndexState} or (throw
    "Unknown Hackage index state ${cachedIndexState}");
  dotCabal = pkgs.haskell-nix.dotCabal {
    index-state = cachedIndexState;
    sha256 = indexSha256;
    nix-tools = pkgs.haskell-nix.nix-tools-unchecked;
  };
  project = planProject.appendModule {
    shell.shellHook = pkgs.lib.mkAfter ''
      cabalCacheRoot="''${XDG_CACHE_HOME:-''${HOME:?HOME must be set}/.cache}"
      export CABAL_DIR="$cabalCacheRoot/agent-daemon/cabal-${cachedIndexState}"
      if [[ ! -e "$CABAL_DIR/config" ]]; then
        mkdir -p "$CABAL_DIR"
        cp -RL ${dotCabal}/. "$CABAL_DIR/"
        chmod -R u+w "$CABAL_DIR"
      fi
    '';
  };
  uiNodeModules = pkgs.importNpmLock.buildNodeModules {
    npmRoot = ./../ui;
    nodejs = pkgs.nodejs_22;
  };
  uiBuild = pkgs.mkSpagoDerivation {
    pname = "agent-daemon-ui-build";
    version = "0.1.0";
    src = ./../ui;
    spagoYaml = ./../ui/spago.yaml;
    spagoLock = ./../ui/spago.lock;
    nativeBuildInputs = [ pkgs.purs pkgs.spago-unstable ];
    buildPhase = ''
      spago build --offline
    '';
    installPhase = ''
      touch $out
    '';
  };
  static = pkgs.mkSpagoDerivation {
    pname = "agent-daemon-static";
    version = "0.1.0";
    src = ./../ui;
    spagoYaml = ./../ui/spago.yaml;
    spagoLock = ./../ui/spago.lock;
    nativeBuildInputs =
      [ pkgs.purs pkgs.spago-unstable pkgs.esbuild pkgs.nodejs_22 ];
    buildPhase = ''
      ln -s ${uiNodeModules}/node_modules node_modules
      mkdir -p dist/fonts
      cp node_modules/@xterm/xterm/css/xterm.css dist/xterm.css
      cp node_modules/@fontsource/jetbrains-mono/files/jetbrains-mono-latin-400-normal.woff2 dist/fonts/
      cp node_modules/@fontsource/noto-sans-symbols-2/files/noto-sans-symbols-2-symbols-400-normal.woff2 dist/fonts/
      esbuild src/bootstrap.js \
        --bundle \
        --outfile=dist/deps.js \
        --format=iife \
        --platform=browser \
        --minify
      spago bundle --offline --module Main --outfile dist/app.js
      cat dist/deps.js dist/app.js > dist/index.js
      rm dist/deps.js dist/app.js
    '';
    installPhase = ''
      mkdir -p $out
      cp -r dist/* $out/
    '';
  };
in {
  components = project.hsPkgs.tmux-ws.components;
  packages = {
    main = project.hsPkgs.tmux-ws.components.exes.tmux-ws;
    tmux-ws = project.hsPkgs.tmux-ws.components.exes.tmux-ws;
    agent-daemon = project.hsPkgs.tmux-ws.components.exes.agent-daemon;
    inherit static uiBuild uiNodeModules;
  };
  devShells.default = project.shell;
}
